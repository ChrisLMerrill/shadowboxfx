plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.7.3'
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

version 1.3
group = 'net.christophermerrill'

javafx {
    version = "12.0.2"
    modules = [ 'javafx.controls' ]
}

repositories
    {
    mavenLocal()
    jcenter()
    }

dependencies
    {
    api 'org.openjfx:javafx-controls:12.0.2'

    testImplementation 'org.testfx:testfx-core:4.0.13-alpha'
    testImplementation 'org.testfx:testfx-junit:4.0.13-alpha'
    testImplementation 'junit:junit:4.12'
    }

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
    }

javadoc {
    include 'com/company/project/pkg/*'
    options.addBooleanOption('html5', true)
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
    }

artifacts {
    archives sourcesJar
    archives javadocJar
    }

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
      tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
      }
    }
  }

wrapper {
    gradleVersion = '5.6.2'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'ShadowboxFx' // pom.project.name must be same as bintray.pkg.name
                description = 'Controls to add shadow-boxed controls to a JavaFX UI.'
                url = 'https://github.com/ChrisLMerrill/shadowboxfx'
                packaging = 'jar'
                groupId = project.group
                artifactId = 'ShadowboxFx'
                version = project.version
                inceptionYear = '2017' // HARDCODED
                licenses {
                    license { // HARDCODED
                        name = 'Apache-2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = "ChrisLMerrill"
                        name = "Chris Merrill"
                        email = "osdev@christophermerrill.net"
                    }
                }
                scm {
                    connection = 'https://github.com/ChrisLMerrill/shadowboxfx.git'
                    developerConnection = 'https://github.com/ChrisLMerrill'
                    url = 'https://github.com/ChrisLMerrill/shadowboxfx'
                }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

bintray {
    user = BINTRAY_UPLOAD_USERNAME
    key = BINTRAY_UPLOAD_APIKEY
    pkg {
        repo = 'maven'
        name = 'shadowboxfx'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/ChrisLMerrill/shadowboxfx.git'
        version {
            name = project.version
            released  = new Date()
            gpg {
                sign = true
                passphrase = GPG_JARSIGN_PASSPHRASE
            }
            mavenCentralSync {
                sync = true //[Default: true] Determines whether to sync the version to Maven Central.
                user = MAVENCENTRAL_USERNAME
                password = MAVENCENTRAL_PASSWORD
                close = '1' //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
            }
        }
    configurations = ['archives']
    }
}